---
title: Modules
meta_title: Pyro &mdash; Modules
---

::: insert toc
::: hr


A module is a Pyro file loaded as a library. Modules are loaded using the `import` keyword.

Assume we have a file called `math.pyro` containing math functions:

::: code pyro
    import math;

    var foo = math::abs(-1);

Use the member access operator `::` to access a module's top-level variables, functions, and classes.

The imported module's name becomes a variable in the importing scope.


### Public/Private Members

A module's top-level members (variables, functions, and classes) are private by default
--- use the `pub` keyword to make them public, e.g.

::: code pyro
    pub var number = 123;

    pub def func() {
        return "hello world!";
    }

    pub class Object {
        var value;
    }


* Public members can be accessed from inside or outside the module.
* Private members can only be accessed from inside the module.


### Submodules

Modules can contain submodules. Assume we have a directory called `math` containing a file called `trig.pyro`:

::: code pyro
    import math::trig;

    var foo = trig::cos(1);

The top-level module `math` can live alongside the directory that contains its submodules:

::: code
    root/
    |-- math.pyro
    |-- math/
        |-- trig.pyro

Alternatively, it can live inside the `math` directory in a file called `self.pyro`:

::: code
    root/
    |-- math/
        |-- self.pyro
        |-- trig.pyro

Finally, the top-level `math` module can be empty and simply function as a container for its submodules:

::: code
    root/
    |-- math/
        |-- trig.pyro

Submodules can contain submodules of their own --- there's no hard limit to how deep the nesting can go.



### Parents, Children, Siblings

Imagine we have the following module structure:

::: code
    root/
    |-- math/
        |-- calc.pyro
        |-- self.pyro
        |-- trig.pyro

A parent module can import its child -- e.g. in `root/math/self.pyro`:

::: code pyro
    import math::trig;

A child module can import its parent -- e.g. in `root/math/calc.pyro`:

::: code pyro
    import math;

A child module can import its sibling -- e.g. in `root/math/calc.pyro`:

::: code pyro
    import math::trig;



### Circular Imports

In general, circular imports are fine --- module `foo` can import module `bar` and module `bar` can import module `foo`.

Do be aware that trying to *use* a member from an imported module that hasn't finished initializing can result in a panic.

Note that you can avoid the problems that sometimes result from top-level circular imports by importing the required module inside a function, e.g.

::: code pyro
    def do_some_math(value) {
        import math;
        return math::abs(value) * math::pi;
    }

This import will only fire when the function is run. (Imported modules are cached so the module won't be re-imported every time the function runs.)



### Aliasing

You can import a module under an alias using an `import ... as ...` statement:

::: code pyro
    import math as alias;

    var foo = alias::abs(-1);

Submodules can similarly be aliased:

::: code pyro
    import math::trig as alias;

    var foo = alias::cos(1);



### Importing Members

You can import a top-level member from a module by wrapping its name in braces, e.g.

::: code pyro
    import math::{abs};

    assert abs(-1) == 1;

You can alias the imported member using an `import ... as ...` statement, e.g.

::: code pyro
    import math::{abs} as foo;

    assert foo(-1) == 1;

You can import multiple members in a single `import` statement by separating their names with commas, e.g.

::: code pyro
    import math::{cos, sin};

You can alias the imported members using a comma-separated `as` list, e.g.

::: code pyro
    import math::{cos, sin} as foo, bar;

You can import *all* the top-level members from a module using an asterisk, e.g.

::: code pyro
    import math::{*};

Note that you can only use this `{*}` syntax at global scope in a script or module file --- i.e. you can't import `{*}` inside a block or function.



### Import Roots

The global `$roots` vector contains the list of root directories that Pyro checks when attempting to import a module.

(*) When Pyro is executing a script it adds the parent directory containing that script to the `$roots` vector, along with a `modules` directory within that parent directory. (This means that the modules required by a script can be located either in the same directory as the script or in a `modules` directory alongside the script.)

(*) When Pyro is executing the REPL it adds the current working directory to the `$roots` vector.

You can add or remove entries from the `$roots` vector to customize Pyro's default behaviour.



### Executing Module Directories

You can execute a module directory if it contains a `self.pyro` file:

    $ pyro path/to/module/directory

Pyro executes the `self.pyro` file, then the `$main()` function if it finds one.
