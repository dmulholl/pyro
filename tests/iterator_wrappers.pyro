assert $is_iter("abc":chars());

var chars1 = "abc":chars():to_vec();
assert chars1:count() == 3;
assert chars1[0] == 'a';
assert chars1[1] == 'b';
assert chars1[2] == 'c';

var chars2 = "abc":chars():enumerate():to_vec();
assert chars2:count() == 3;
assert chars2[0] == $tup(0, 'a');
assert chars2[1] == $tup(1, 'b');
assert chars2[2] == $tup(2, 'c');

def add_one(n) {
    return n + 1;
}

var nums1 = $iter([1, 2, 3]):map(add_one):to_vec();
assert nums1:count() == 3;
assert nums1[0] == 2;
assert nums1[1] == 3;
assert nums1[2] == 4;

var nums2 = $iter([1, 2, 3]):map(add_one):map(add_one):to_vec();
assert nums2:count() == 3;
assert nums2[0] == 3;
assert nums2[1] == 4;
assert nums2[2] == 5;

def is_big(n) {
    return n >= 5;
}

var nums3 = $iter([2, 4, 6, 8]):filter(is_big):to_vec();
assert nums3:count() == 2;
assert nums3[0] == 6;
assert nums3[1] == 8;

var nums4 = $iter([2, 4, 6, 8]):map(add_one):filter(is_big):to_vec();
assert nums4:count() == 3;
assert nums4[0] == 5;
assert nums4[1] == 7;
assert nums4[2] == 9;

class Counter {
    var stop;
    var next = 0;

    def $init(stop) {
        self.stop = stop;
    }

    def $iter() {
        return self;
    }

    def $next() {
        if self.next < self.stop {
            self.next += 1;
            return self.next - 1;
        }
        return $err();
    }
}

var counter = $iter(Counter(4)):to_vec();
assert counter:count() == 4;
assert counter[0] == 0;
assert counter[1] == 1;
assert counter[2] == 2;
assert counter[3] == 3;

def is_even(n) {
    return n % 2 == 0;
}

def square(n) {
    return n * n;
}

var vec = $range(10):filter(is_even):map(square):to_vec();
assert vec:count() == 5;
assert vec[0] == 0;
assert vec[1] == 4;
assert vec[2] == 16;
assert vec[3] == 36;
assert vec[4] == 64;


def $test_skip_first() {
    var vec = $range(5):skip_first(3):to_vec();
    assert vec:count() == 2;
    assert vec[0] == 3;
    assert vec[1] == 4;
}


def $test_skip_last() {
    var vec = $range(5):skip_last(3):to_vec();
    assert vec:count() == 2;
    assert vec[0] == 0;
    assert vec[1] == 1;
}


def $test_to_set() {
    var set = "abcabcabc":chars():to_set();
    assert $is_set(set);
    assert set:count() == 3;
    assert set:contains('a');
    assert !set:contains('z');
}


def $test_next() {
    var iter = $range(3);
    assert iter:next() == 0;
    assert iter:next() == 1;
    assert iter:next() == 2;
    assert $is_err(iter:next());
}


def $test_join() {
    assert $range(3):join() == "012";
    assert $range(3):join(",") == "0,1,2";
    assert $is_err(try $range(3):join(123));
    assert $is_err(try $range(3):join(",", 123));
}


def $test_sum() {
    assert $range(5):sum() == 0 + 1 + 2 + 3 + 4;
    assert []:iter():sum() == null;
    assert [1, 2, 3]:iter():sum() == 1 + 2 + 3;
    assert [1, 2, 3.0]:iter():sum() == 1 + 2 + 3.0;
    assert ["foo", "bar", "baz"]:iter():sum() == "foobarbaz";
    assert $is_err(try [123, "foo"]:iter():sum());
}


def $test_product() {
    assert $range(5):product() == 0 * 1 * 2 * 3 * 4;
    assert []:iter():product() == null;
    assert [1, 2, 3]:iter():product() == 1 * 2 * 3;
    assert [1, 2, 3.0]:iter():product() == 1 * 2 * 3.0;
    assert $is_err(try [123, "foo"]:iter():product());
}


def $test_reduce() {
    assert [1, 2, 3, 4]:iter():reduce(def(acc, value) {return acc + value;}, 0) == 10;
    assert []:iter():reduce(def(acc, value) {return acc + value;}, 0) == 0;
    assert [1, 2, 3, 4]:iter():reduce(def(acc, value) {return acc * value;}, 1) == 24;
    assert []:iter():reduce(def(acc, value) {return acc * value;}, 1) == 1;
}
