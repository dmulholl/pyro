assert $is_map("foobar") == false;

var foo = $map();
assert $is_map(foo) == true;
assert foo:count() == 0;

foo:set("alpha", 123);
assert foo:count() == 1;

foo:set("beta", 456);
assert foo:count() == 2;

foo:set("gamma", 789);
assert foo:count() == 3;

assert foo:get("alpha") == 123;
assert foo:get("beta") == 456;
assert foo:get("gamma") == 789;

foo:set(1, null);
foo:set(2, true);
foo:set(3, false);

assert foo:count() == 6;
assert foo:get(1) == null;
assert foo:get(2) == true;
assert foo:get(3) == false;

foo:set(4, "foo");
foo:set(4.0, "bar");

assert foo:get(4) == "foo";
assert foo:get(4.0) == "bar";

assert $is_err(foo:get("undefined"));

var bound_method = foo:get;
assert bound_method("alpha") == 123;
assert bound_method(4) == "foo";

assert foo:count() == 8;
foo:del("alpha");
assert foo:count() == 7;
assert $is_err(foo:get("alpha"));

assert foo:contains("beta") == true;
assert foo:contains("undefined") == false;


def $test_literal() {
    var map = {
        "abc" = 123,
        "def" = 456,
    };

    assert $is_map(map);
    assert map:count() == 2;
    assert map:get("abc") == 123;
    assert map:get("def") == 456;
}


def $test_indexing() {
    var map = $map();
    map["abc"] = 123;
    map["def"] = 456;

    assert map["abc"] == 123;
    assert map["def"] == 456;

    map["abc"] = 789;
    assert map["abc"] == 789;
}


def $test_stress() {
    var map = $map();

    for i in $range(1000) {
        map[i] = i;
    }

    assert map[123] == 123;
    assert map[456] == 456;
    assert map[789] == 789;
}


def $test_copy() {
    var map = {
        "abc" = 123,
        "def" = 456,
    };

    var copy = map:copy();

    assert copy:count() == 2;
    assert copy["abc"] == 123;
    assert copy["def"] == 456;
}


def $test_keys_iter() {
    var map = {
        "abc" = 123,
        "def" = 456,
    };

    var keys = map:keys():to_vec();
    assert keys:count() == 2;
    assert keys[0] == "abc";
    assert keys[1] == "def";
}


def $test_values_iter() {
    var map = {
        "abc" = 123,
        "def" = 456,
    };

    var values = map:values():to_vec();
    assert values:count() == 2;
    assert values[0] == 123;
    assert values[1] == 456;
}


def $test_entries_iter() {
    var map = {
        "abc" = 123,
        "def" = 456,
    };

    var entries = map:entries():to_vec();
    assert entries:count() == 2;
    assert entries[0] == $tup("abc", 123);
    assert entries[1] == $tup("def", 456);
}


def $test_is_empty() {
    var map1 = {
        "abc" = 123,
        "def" = 456,
    };
    assert !map1:is_empty();

    var map2 = {};
    assert map2:is_empty();
}
