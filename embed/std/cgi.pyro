import std::json;
import std::url;

pub class Request {
    var params: map[str, vec[str]];
    var body: str;

    def $init() {
        self.params = $map();
        self.body = $stdin:read_string();

        if $env("REQUEST_METHOD") in ("POST", "PUT", "PATCH", "DELETE") {
            if $env("CONTENT_TYPE") == "application/x-www-form-urlencoded" {
                self:unpack_url_encoded_string(self.body);
            }
        }

        var query_string = $env("QUERY_STRING");
        if $is_str(query_string) {
            self:unpack_url_encoded_string(query_string);
        }
    }

    pub def body() -> str {
        return self.body;
    }

    pub def found(key: str) -> bool {
        return key in self.params;
    }

    pub def value(key: str) -> str|err {
        var values = self.params[key];
        if $is_err(values) {
            return $err("key '${key}' not found in request parameters");
        }

        return try values:last();
    }

    pub def values(key: str) -> vec[str] {
        return self.params[key] !! [];
    }

    pub def params() -> map[str, vec[str]] {
        return self.params;
    }

    pub def from_json() -> map|vec|str|i64|f64|bool|null|err {
        var result = try json::from_json(self.body);
        if $is_err(result) {
            return $err("request body is not valid json: ${result}");
        }

        return result;
    }

    def unpack_url_encoded_string(input: str) {
        for element in input:split("&") {
            if element:contains("=") {
                var parts = element:split("=");

                var key = url::url_decode(parts[0]);
                if $is_err(key) {
                    continue;
                }

                var value = url::url_decode(parts[1]);
                if $is_err(value) {
                    continue;
                }

                var values = self.params[key];
                if $is_err(values) {
                    self.params[key] = [value];
                } else {
                    values:append(value);
                }
            }
        }
    }
}

pub class Response {
    var headers: vec[str];
    var body: buf;

    def $init() {
        self.headers = $vec();
        self.body = $buf();
    }

    pub def add_header(header: str) -> Response {
        self.headers:append(header);
        return self;
    }

    pub def make_301_redirect(url: str) -> Response {
        self:add_header("Status: 301 Moved Permanently");
        self:add_header("Location: " + url);
        return self;
    }

    pub def make_302_redirect(url: str) -> Response {
        self:add_header("Status: 302 Found");
        self:add_header("Location: " + url);
        return self;
    }

    pub def make_303_redirect(url: str) -> Response {
        self:add_header("Status: 303 See Other");
        self:add_header("Location: " + url);
        return self;
    }

    pub def make_307_redirect(url: str) -> Response {
        self:add_header("Status: 307 Temporary Redirect");
        self:add_header("Location: " + url);
        return self;
    }

    pub def write(*args) -> Response {
        self.body:write(*args);
        return self;
    }

    pub def send() {
        for header in self.headers {
            let line = header:strip():replace("\r", "\\r"):replace("\n", "\\n");
            $print("{}\r\n", line);
        }

        $print("\r\n");
        $print(self.body);
    }
}
