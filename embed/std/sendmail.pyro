pub class Email {
    var headers: vec[str];
    var to_addr: str;
    var from_addr: str;
    var subject: str;
    var body: buf;

    def $init() {
        self.headers = $vec();
        self.body = $buf();
    }

    def $str() {
        var msg = self:make_message();
        if $is_err(msg) {
            return $fmt("<instance Email: {}>", msg);
        }
        return msg:to_str();
    }

    pub def to(email_addr: str) -> Email {
        self.to_addr = email_addr;
        return self;
    }

    pub def from(email_addr: str) -> Email {
        self.from_addr = email_addr;
        return self;
    }

    pub def subject(value: str) -> Email {
        self.subject = value;
        return self;
    }

    pub def add_header(header: str) -> Email {
        self.headers:append(header);
        return self;
    }

    pub def write(*args) -> Email {
        self.body:write(*args);
        return self;
    }

    pub def send() -> err? {
        var msg = self:make_message();
        if $is_err(msg) {
            return msg;
        }

        var result = try $shell("sendmail -i -t", msg);
        if $is_err(result) {
            return result;
        }

        var (exit_code, _, error_output) = result;
        if exit_code != 0 {
            return $err(error_output);
        }
    }

    def make_message() -> buf|err {
        var msg = $buf();

        if $is_str(self.to_addr) && !self.to_addr:is_empty_or_ws() {
            var value = self.to_addr:strip():replace("\r", "\\r"):replace("\n", "\\n");
            msg:write("To: {}\r\n", value);
        } else {
            return $err("invalid email: missing 'to' address");
        }

        if $is_str(self.from_addr) && !self.from_addr:is_empty_or_ws() {
            var value = self.from_addr:strip():replace("\r", "\\r"):replace("\n", "\\n");
            msg:write("From: {}\r\n", value);
        } else {
            return $err("invalid email: missing 'from' address");
        }

        if $is_str(self.subject) && !self.subject:is_empty_or_ws() {
            var value = self.subject:strip():replace("\r", "\\r"):replace("\n", "\\n");
            msg:write("Subject: {}\r\n", value);
        } else {
            return $err("invalid email: missing 'subject' field");
        }

        for header in self.headers {
            msg:write("{}\r\n", header:strip():replace("\r", "\\r"):replace("\n", "\\n"));
        }

        msg:write("\r\n");
        msg:write($str(self.body):strip());

        return msg;
    }
}
