import std::fs;

var init_script = "
var $filepath;

pub def set_filepath(filepath) {
    $filepath = filepath;
}
";

def make_module(filepath) {
    var module = $exec(init_script);
    module::set_filepath(filepath);
    return module;
}

def add_import_roots_from_path(filepath) {
    var stashed_roots = $roots:copy();

    var real_path = try fs::realpath(filepath);
    if $is_err(real_path) {
        $exit("error: failed to resolve path '${filepath}': ${code}");
    }

    var parent_path = fs::dirname(real_path);
    var modules_path = fs::join(parent_path, "modules");

    $roots:append(parent_path);
    $roots:append(modules_path);

    return stashed_roots;
}

def restore_stashed_import_roots(stashed_roots) {
    $roots:clear();

    for root in stashed_roots {
        $roots:append(root);
    }
}

def time_function(file_name, func_name, func, is_first) {
    if !is_first {
        echo;
    }

    echo file_name, "::", func_name;
    var times = [];

    for i in $range(NUM_RUNS) {
        var start_time = $clock();
        var result = try func();
        var end_time = $clock();

        if $is_err(result) {
            $exit("    error: ${result}");
        }

        times:append(end_time - start_time);
    }

    var mean_time_s = times:iter():sum() / times:count();
    var mean_time_ms = mean_time_s * 1000;
    echo "    mean: ${mean_time_ms;.3f} ms";
}

if NUM_RUNS < 1 {
    $exit("error: invalid value for --num-runs, must be >= 1");
}

var is_first = true;

for filepath in $args {
    if !fs::is_file(filepath) {
        $exit("error: invalid path '${filepath}'");
    }

    var code = try $read_file(filepath);
    if $is_err(code) {
        $exit("error: failed to read file '${filepath}': ${code}");
    }

    var stashed_roots = add_import_roots_from_path(filepath);

    var module = make_module(filepath);
    var result = try $exec(code, filepath, module);
    if $is_err(result) {
        $exit("error: failed to execute file '${filepath}': ${result}");
    }

    for name in module:all_members() {
        if name:starts_with("$time_") && $is_func(module:member(name)) {
            time_function(filepath, name, module:member(name), is_first);
            is_first = false;
        }
    }

    restore_stashed_import_roots(stashed_roots);
}

